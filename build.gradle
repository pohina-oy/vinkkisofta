apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'checkstyle'
apply plugin: "jacoco"

project.ext {
    junitVersion = '4.12'
    seleniumVersion = '2.41.0'
}

dependencies {
    testCompile 'junit:junit:' + junitVersion
    testCompile 'org.mockito:mockito-core:2.10.0'
    compile 'com.google.guava:guava:20.0'
    compile 'com.sparkjava:spark-core:2.8.0'
    compile 'com.sparkjava:spark-template-thymeleaf:2.7.1'
    compile 'org.thymeleaf.extras:thymeleaf-extras-java8time'
    compile 'org.slf4j:slf4j-simple:1.7.25'
    testCompile 'io.cucumber:cucumber-java:4.2.0'
    testCompile 'io.cucumber:cucumber-junit:4.2.0'
    compile group: 'org.seleniumhq.selenium', name: 'selenium-api', version:seleniumVersion
    compile group: 'org.seleniumhq.selenium', name: 'selenium-java', version:seleniumVersion
    compile group: 'postgresql', name: 'postgresql', version: '9.0-801.jdbc4'
    compile group: 'io.github.cdimascio', name: 'java-dotenv', version: '3.1.7'
    compile 'org.apache.httpcomponents:fluent-hc:4.5.6'
    compile 'com.google.code.gson:gson:2.8.5'
}

repositories {
    jcenter()
    mavenCentral()
}

test {
    testLogging.showStandardStreams = true
    systemProperties System.getProperties()

    jacoco {
        excludes += ['com/steadystate/css/**']
    }
}

run {}

jar {
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }

    manifest {
        attributes 'Main-Class': 'fi.pohina.vinkkilista.Main'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                exclude: [
                    'fi/pohina/vinkkilista/App.*',
                    'fi/pohina/vinkkilista/Main.*',
                    'fi/pohina/vinkkilista/api/GithubJsonApi.*',
                    'fi/pohina/vinkkilista/api/GithubOAuthApi.*',
                    'fi/pohina/vinkkilista/data_access/Postgres*'
                ]
            )
        })
    }
}

task stage(dependsOn: ['jar', 'clean'])
jar.mustRunAfter clean

mainClassName = 'fi.pohina.vinkkilista.Main'
